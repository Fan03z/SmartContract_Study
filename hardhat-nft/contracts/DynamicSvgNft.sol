// SPDX-License-Identifier: MIT
pragma solidity ^0.8.7;

// å°†svgè½¬æ¢ä¸ºbase64ç¼–ç çš„åœ°å€: https://www.base64encode.org/
// è¦é€šè¿‡base64ç¼–ç æŸ¥çœ‹svgå›¾åƒ,è¾“å…¥ç½‘å€:   data:image/svg+xml;base64,   + å¯¹åº”base64ç¼–ç 
// è¦é€šè¿‡base64ç¼–ç æŸ¥çœ‹JSONæ–‡ä»¶,è¾“å…¥ç½‘å€:   data:application/json;base64,   + å¯¹åº”base64ç¼–ç 

// é€šè¿‡@openzeppelin/contractsåŒ…å¯¼å…¥ERC721æ ‡å‡†åˆçº¦
import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
// å¯¼å…¥base64-solåŒ…å†…çš„base64.sol
import "base64-sol/base64.sol";
// å¯¼å…¥@chainlink/contractsåŒ…å†…çš„AggregatorV3Interface.sol,æ¥æŸ¥è¯¢æ±‡ç‡ä»·æ ¼
import "@chainlink/contracts/src/v0.8/interfaces/AggregatorV3Interface.sol";

// å®šä¹‰å¯¹åº”IDçš„ä»¤ç‰Œå·²å­˜åœ¨æç¤ºé”™è¯¯
error ERC721Metadata__URI_QueryFor_NonExistentToken();

contract DynamicSvgNft is ERC721 {
    // ä¸‹æ–¹éƒ¨åˆ†æ­¥éª¤ä¸BasicNft.solåŒ
    uint256 private s_tokenCounter;
    // å®šä¹‰å›¾åƒURI
    string private i_lowImageURI;
    string private i_highImageURI;
    // å®šä¹‰svgåœ¨base64ç¼–ç ä¸‹çš„URIå¤´éƒ¨
    string private constant base64EncodedSvgPrefix =
        "data:image/svg+xml;base64,";
    // å®šä¹‰AggregatorV3Interfaceå¯¹è±¡
    AggregatorV3Interface internal immutable i_priceFeed;
    // å®šä¹‰ä¸åŒé“¸é€ è€…ç»™å‡ºçš„NFTå˜åŒ–çš„ä»·æ ¼
    mapping(uint256 => int256) public s_tokenIdToHighValue;

    // å®šä¹‰é“¸é€ NFTäº‹ä»¶
    event CreatedNFT(uint256 indexed tokenId, int256 highValue);

    // ä¼ å…¥svgå›¾åƒçš„base64ç¼–ç çš„å›¾åƒurlæˆ–uriåšå‚æ•°
    constructor(
        address priceFeedAddress,
        string memory lowSvg,
        string memory highSvg
    ) ERC721("Dynamic Svg Nft", "DSN") {
        s_tokenCounter = 0;
        // å°†å¼€å¿ƒå’Œçš±çœ‰ä¸¤ä¸ªsvgå›¾åƒè½¬æ¢ä¸ºURI
        i_lowImageURI = svgToImageURI(lowSvg);
        i_highImageURI = svgToImageURI(highSvg);
        // èµ‹å€¼
        i_priceFeed = AggregatorV3Interface(priceFeedAddress);
    }

    // å®ç°å°†svgä»£ç è½¬æ¢ä¸ºå›¾åƒURI(åŒ…å«äº†è½¬æ¢å¾—åˆ°çš„base64ç¼–ç )
    function svgToImageURI(
        string memory svg
    ) public pure returns (string memory) {
        // ğŸ¤¡ é…åˆcontracts/sublessonç†è§£ abi.encode() å’Œ abi.encodePacked()
        string memory svgBase64Encodeed = Base64.encode(
            bytes(string(abi.encodePacked(svg)))
        );
        return
            string(abi.encodePacked(base64EncodedSvgPrefix, svgBase64Encodeed));
    }

    function mintNft(int256 highValue) public {
        // è®°å½•ä¸åŒä»¤ç‰ŒIdè®¾ç½®çš„NFTè½¬æ¢çš„ä»·æ ¼
        s_tokenIdToHighValue[s_tokenCounter] = highValue;
        _safeMint(msg.sender, s_tokenCounter);
        s_tokenCounter++;
        // è§¦å‘CreatedNFT()äº‹ä»¶
        emit CreatedNFT(s_tokenCounter, highValue);
    }

    // å¾—åˆ°é€šè¿‡base64ç¼–ç æŸ¥çœ‹JSONæ–‡ä»¶çš„ç½‘å€å‰ç¼€
    // // è¦†ç›–ERC721.solä¸Šçš„_baseURI()
    function _baseURI() internal pure override returns (string memory) {
        return "data:application/json;base64,";
    }

    // å¾—åˆ°ä»¤ç‰ŒURI
    // è¦†ç›–ERC721.solä¸Šçš„tokenURI()
    function tokenURI(
        uint256 tokenId
    ) public view override returns (string memory) {
        // ERC721æä¾›_exists(),æŸ¥è¯¢å¯¹åº”IDçš„ä»¤ç‰Œæ˜¯å¦å­˜åœ¨,å·²å­˜åœ¨åˆ™æç¤ºé”™è¯¯
        if (!_exists(tokenId)) {
            revert ERC721Metadata__URI_QueryFor_NonExistentToken();
        }

        // å®šä¹‰å¯¼å…¥å›¾åƒURI:
        // è§£æ„AggregatorV3Interfaceå¯¹è±¡,è·å¾—æœ€æ–°æ±‡ç‡ä»·æ ¼ä¿¡æ¯
        (, int256 price, , , ) = i_priceFeed.latestRoundData();
        // é€šè¿‡å½“å‰æ±‡ç‡ä»·æ ¼ä¿¡æ¯å˜åŒ–,æ§åˆ¶NFTå‘ç”Ÿå˜åŒ–
        string memory imageURI = i_lowImageURI;
        // å½“ä»·æ ¼é«˜äºæˆ–ç­‰äºé“¸é€ æ—¶è®¾å®šä»·æ ¼æ—¶,NFTè½¬æ¢ä¸ºå¼€å¿ƒ,å¦åˆ™ä¸ºçš±çœ‰
        if (price >= s_tokenIdToHighValue[tokenId]) {
            imageURI = i_highImageURI;
        }

        // è½¬æ¢ä¸ºå­—ç¬¦ä¸²è¿”å›
        return
            string(
                // åŠ ä¸Šbase64ç¼–ç JSONå½¢å¼çš„ç½‘å€å‰ç¼€
                abi.encodePacked(
                    _baseURI(),
                    // è½¬æ¢ä¸ºå­—èŠ‚ç±»å‹,å¹¶è¿›è¡Œbase64ç¼–ç 
                    Base64.encode(
                        bytes(
                            // è¿æ¥å¹¶ç¼–ç URIå†…å®¹
                            // ERC721æä¾›name(),æŸ¥è¯¢ä»¤ç‰Œåç§°
                            abi.encodePacked(
                                '{"name":"',
                                name(),
                                '", "description":"An NFT that changes based on the Chainlink Feed", ',
                                '"attributes": [{"trait_type": "coolness", "value": 100}], "image":"',
                                imageURI,
                                '"}'
                            )
                        )
                    )
                )
            );
    }

    // get()
    function getLowSVG() public view returns (string memory) {
        return i_lowImageURI;
    }

    function getHighSVG() public view returns (string memory) {
        return i_highImageURI;
    }

    function getPriceFeed() public view returns (AggregatorV3Interface) {
        return i_priceFeed;
    }

    function getTokenCounter() public view returns (uint256) {
        return s_tokenCounter;
    }
}
